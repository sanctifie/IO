# 8 Creating DB subnet group for RDS Instances
resource "aws_db_subnet_group" "db_subnet_group" {
  name       = var.sg-name
  subnet_ids = [data.aws_subnet.private-subnet1.id, data.aws_subnet.private-subnet2.id]
}


# Creating Aurora RDS Cluster, username and password used only for practice, otherwise follow DevOps best practices to keep it secret
resource "aws_rds_cluster" "aurora_cluster" {
  cluster_identifier = "aurora-cluster"
  engine             = "aurora-mysql"
  # engine_version : laissé à la valeur par défaut d'AWS (la 3.02.2 d'origine est hors support)
  master_username         = var.rds-username
  master_password         = var.rds-pwd
  backup_retention_period = 1
  preferred_backup_window = "07:00-09:00"
  skip_final_snapshot     = true
  database_name           = var.db-name
  port                    = 3306
  db_subnet_group_name    = aws_db_subnet_group.db_subnet_group.name
  vpc_security_group_ids  = [data.aws_security_group.db-sg.id]
  # Aurora Serverless v2 : de 0,5 à 2 ACU selon la charge (au lieu de 2 × db.r5.large ≈ 0,58 $/h)
  serverlessv2_scaling_configuration {
    min_capacity = 0.5
    max_capacity = 2
  }
  tags = {
    Name = var.rds-name
  }
}

# Creating RDS Cluster instance
resource "aws_rds_cluster_instance" "primary_instance" {
  cluster_identifier = aws_rds_cluster.aurora_cluster.id
  identifier         = "primary-instance"
  instance_class     = "db.serverless"
  engine             = aws_rds_cluster.aurora_cluster.engine
  engine_version     = aws_rds_cluster.aurora_cluster.engine_version
}

# Creating RDS Read Replica Instance
resource "aws_rds_cluster_instance" "read_replica_instance" {
  count              = var.read_replicas # 0 par défaut pour l'apprentissage ; 1 pour tester la haute disponibilité
  cluster_identifier = aws_rds_cluster.aurora_cluster.id
  identifier         = "read-replica-instance-${count.index}"
  instance_class     = "db.serverless"
  engine             = aws_rds_cluster.aurora_cluster.engine

  depends_on = [aws_rds_cluster_instance.primary_instance]
}